Функция ПолучитьИмяФайла(ПервыйКод, ВторойКод)	
	ВремКаталог = КаталогВременныхФайлов();
	// А вот и тернарник!
	// Кстати, жаль, что 1С не дает что-то типа Путь.Объединить(путь1, путь2).
	// Но если руководствоваться мыслью "редко нужно - вообще не нужно"... то их понять можно.
	// Алсо, линукс с таким вот в пролете. "/" же, а не "\"!
	ИмяФайла = ВремКаталог + ?(Прав(ВремКаталог, 1) = "\", "", "\")
		+ "Message_" + СокрЛП(ПервыйКод)
		+ "_" + СокрЛП(ВторойКод)
		+ ".xml";
	Возврат ИмяФайла;
КонецФункции

Процедура ЗаписатьСообщениеСИзменениями() Экспорт
	Сообщения.Инфо("-------- Выгрузка в узел " + Строка(ЭтотОбъект) + " ------------");

	ИмяФайла = ПолучитьИмяФайла(ПланыОбмена.Филиалы.ЭтотУзел().Код, Ссылка.Код);
	// А жсоном можно? Есть же ЗаписьJSON!
	ЗаписьXML = новый ЗаписьXML();
	ЗаписьXML.ОткрытьФайл(ИмяФайла);
	ЗаписьXML.ЗаписатьОбъявлениеXML();
	
	ЗаписьСообщения = ПланыОбмена.СоздатьЗаписьСообщения();
	ЗаписьСообщения.НачатьЗапись(ЗаписьXML, Ссылка); // таки нельзя, ожидает именно иксемель
	Сообщения.Инфо("Номер сообщения: " + ЗаписьСообщения.НомерСообщения);
	
	ВыборкаИзменений = ПланыОбмена.ВыбратьИзменения(ЗаписьСообщения.Получатель, ЗаписьСообщения.НомерСообщения);
	Пока ВыборкаИзменений.Следующий() цикл
		ЗаписатьXML(ЗаписьXML, ВыборкаИзменений.Получить());
	КонецЦикла;
	
	ЗаписьСообщения.ЗакончитьЗапись();
	
	ЗаписьXML.Закрыть();
		
	Сообщения.Инфо("-------- Конец выгрузки ------------");
КонецПроцедуры


Процедура ПрочитатьСообщениеСИзменениями() Экспорт
	ИмяФайла = ПолучитьИмяФайла(Ссылка.Код, ПланыОбмена.Филиалы.ЭтотУзел().Код);
	Файл = новый Файл(ИмяФайла);
	Если не Файл.Существует() тогда возврат конецесли;
	
	ЧтениеXML = новый ЧтениеXML();
	Попытка
		ЧтениеXML.ОткрытьФайл(ИмяФайла);
	Исключение
		Сообщения.Инфо("Невозможно открыть файл обмена данными.");
		Возврат;
	КонецПопытки;
	
	Сообщения.Инфо("-------- Загрузка из " + Строка(ЭтотОбъект) + " ------------");
	Сообщения.Инфо(" – Считывается файл: "+ ИмяФайла);
	
	ЧтениеСообщения = ПланыОбмена.СоздатьЧтениеСообщения();
	ЧтениеСообщения.НачатьЧтение(ЧтениеXML);
	
	Если ЧтениеСообщения.Отправитель <> Ссылка тогда
		ВызватьИсключение "Неверный узел";
	конецесли;
	
	ПланыОбмена.УдалитьРегистрациюИзменений(
		ЧтениеСообщения.Отправитель,
		ЧтениеСообщения.НомерПринятого);
	
	Пока ВозможностьЧтенияXML(ЧтениеXML) цикл
		Данные = ПрочитатьXML(ЧтениеXML);
		Если не ЧтениеСообщения.Отправитель.Главный и
			ПланыОбмена.ИзменениеЗарегистрировано(ЧтениеСообщения.Отправитель, Данные)
		тогда
			Сообщения.Инфо(" — Изменения отклонены");
			продолжить;
		КонецЕсли;
		
		Данные.ОбменДанными.Отправитель = ЧтениеСообщения.Отправитель;
		Данные.ОбменДанными.Загрузка = Истина;
		Данные.Записать();
	КонецЦикла;
	
	ЧтениеСообщения.ЗакончитьЧтение();
	
	ЧтениеXML.Закрыть();
	
	УдалитьФайлы(Файл);
	Сообщения.Инфо("-------- Конец загрузки ------------");
КонецПроцедуры