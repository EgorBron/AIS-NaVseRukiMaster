Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ОбменДанными.Загрузка Тогда // требуется
         Возврат;
    КонецЕсли;
	ОбновитьДатуДвижений = ЭтоНовый() Или Движения.ОстаткиМатериалов.Модифицированность();
	Если Не ОбновитьДатуДвижений Тогда
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ТекущийДокумент", Ссылка);
		Запрос.Текст =
		"ВЫБРАТЬ
		| Дата
		|ИЗ
		| Документ.ВводНачальныхОстатковНоменклатуры
		|ГДЕ
		| Ссылка = &ТекущийДокумент";
		Выборка = Запрос.Выполнить().Выбрать();
		Выборка.Следующий();
		ОбновитьДатуДвижений = Выборка.Дата <> Дата;
	КонецЕсли;
	Если ОбновитьДатуДвижений Тогда
		Если Не Движения.ОстаткиМатериалов.Выбран() И Не Движения.ОстаткиМатериалов.Модифицированность() Тогда
			Движения.ОстаткиМатериалов.Прочитать();
		КонецЕсли;
		Для Каждого ЗаписьРегистра Из Движения.ОстаткиМатериалов Цикл
			ЗаписьРегистра.Период = Дата;
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры