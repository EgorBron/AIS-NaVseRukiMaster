
&НаСервереБезКонтекста
Процедура СформироватьНаСервере(Диаграмма)
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	НФПД.Сотрудник,
	|	НФПД.ВидРасчета,
	|	НФПД.ПериодДействияНачало,
	|	НФПД.ПериодДействияКонец,
	|	НФПД.Результат,
	|	НФПД.Регистратор,
	|	НФПД.Регистратор.Представление
	|ИЗ
	|	РегистрРасчета.Начисления.ФактическийПериодДействия КАК НФПД");
	
	Выборка = Запрос.Выполнить().Выбрать();
	Диаграмма.Обновление = Ложь;
	Диаграмма.Очистить();
	Диаграмма.ОтображатьЗаголовок = Ложь;
	
	Пока Выборка.Следующий() Цикл
		ТекущаяСерия = Диаграмма.УстановитьСерию(Выборка.ВидРасчета);
		ТекущаяТочка = Диаграмма.УстановитьТочку(Выборка.Сотрудник);
		ТекущееЗначение = Диаграмма.ПолучитьЗначение(ТекущаяТочка, ТекущаяСерия);
		ТекущийИнтервал = ТекущееЗначение.Добавить();
		ТекущийИнтервал.Начало = Выборка.ПериодДействияНачало;
		ТекущийИнтервал.Конец = Выборка.ПериодДействияКонец;
		ТекущийИнтервал.Текст = Выборка.РегистраторПредставление;
		ТекущийИнтервал.Расшифровка = Выборка.Регистратор;
	КонецЦикла;
	
	Для Каждого Серия из Диаграмма.Серии Цикл
		Если Серия.Значение = ПланыВидовРасчета.ОсновныеНачисления.Оклад Тогда
			Серия.Цвет = WEBЦвета.Желтый;
		ИначеЕсли Серия.Значение = ПланыВидовРасчета.ОсновныеНачисления.Премия Тогда
			Серия.Цвет = WEBЦвета.Зеленый;
		ИначеЕсли Серия.Значение = ПланыВидовРасчета.ОсновныеНачисления.Невыход Тогда
			Серия.Цвет = WEBЦвета.Красный;
		КонецЕсли;
	КонецЦикла;

	Диаграмма.Обновление = Истина;
КонецПроцедуры

&НаКлиенте
Процедура Сформировать(Команда)
	СформироватьНаСервере(ДиаграммаГанта);
КонецПроцедуры
